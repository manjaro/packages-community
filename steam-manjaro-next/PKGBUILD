# Maintainer: Fabian Bornschein <plusfabi(At)gmail(Dot)com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
# Contributor: Alexandru Ianu <alexandru@manjaro.org>
# Contributor: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>

pkgname=steam-manjaro
_package=steam
pkgver=1.0.0.54
pkgrel=1
pkgdesc="Valve's digital software delivery system"
url='http://steampowered.com/'
arch=('i686' 'x86_64')
license=('custom')
depends=('lsb-release' 'desktop-file-utils' 'curl' 'dbus' 'freetype2' 'gdk-pixbuf2' 'ttf-font' 'zenity')
depends_x86_64=('lib32-libgl' 'lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss' 'lib32-alsa-plugins')
optdepends=('steam-native: steam native runtime support')
provides=('steam-manjaro' 'steam')
conflicts=('steam-manjaro' 'steam')
install=steam-manjaro.install
source=(http://repo.steampowered.com/${_package}/pool/${_package}/s/${_package}/${_package}_${pkgver}.tar.gz
        'steam-manjaro.install'
        'steam-uinput.conf'
        '0001-steam_controller_uaccess.patch'
        '0002-steam_htc_vive_mode0660.patch'
        '0003-alsa-sdl-audiodriver.patch'
        '0004-dbus_fatal_warning.patch'
        '0005-manjaro-steam-native.patch'
        '0006-lib_path_and_steam_runtime.patch'
        '0006-lib32_path_and_steam_runtime.patch'
        )
md5sums=('d1398490635615c428165e984a1ec71b'
         '76a80fcfbea2263b8848cc6acf8c58f7'
         '895c8735878bc1611cf6e1ee71f60ee6'
         'a43df36943b5a6da88bc320c1abd0ca1'
         '12e3ca2521b33dac0c668e423a8a7c31'
         '5c05f0f4ba955b7627bb1cde4fdde6ca'
         'e76badfbfe118c102bff2710b66577ec'
         'da4753796db331b2c02f24caa5fb20b0'
         'a36a4a1fc668e99d894efb3f77a23be6'
         'f5beb80a3007d6714060bb3dd2406c55')

prepare()
{
    cd ${_package}
    patch -Np1 < "${srcdir}/0001-steam_controller_uaccess.patch"
    patch -Np1 < "${srcdir}/0002-steam_htc_vive_mode0660.patch"
    patch -Np1 < "${srcdir}/0003-alsa-sdl-audiodriver.patch"
    patch -Np1 < "${srcdir}/0004-dbus_fatal_warning.patch"
    patch -Np1 < "${srcdir}/0005-manjaro-steam-native.patch"
    if [[ "$CARCH" == "x86_64" ]]
        then
            patch -Np1 < "${srcdir}/0006-lib32_path_and_steam_runtime.patch"
        else
            patch -Np1 < "${srcdir}/0006-lib_path_and_steam_runtime.patch"
    fi
}

package()
{
    make -C "${_package}" DESTDIR="${pkgdir}" install

    # install license
    install -Dm 644 "${pkgdir}/usr/share/doc/steam/steam_install_agreement.txt" \
    "${pkgdir}/usr/share/licenses/steam/LICENSE"

    #install changelog
    install -Dm 644 "${_package}/debian/changelog" -t "${pkgdir}/usr/share/doc/${_package}"

    # blank steamdeps because apt-get
    ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

    # install udev rules
    install -Dm 644 "${_package}/lib/udev/rules.d/99-steam-controller-perms.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-controller.rules"
    install -Dm 644 "${_package}/lib/udev/rules.d/60-HTC-Vive-perms.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/70-htc-vive.rules"

    # start uinput at boot
    install -Dm 644 "${srcdir}/steam-uinput.conf" \
    "${pkgdir}/etc/modules-load.d/steam-uinput.conf"

    # License
    install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    ln -s "/usr/share/doc/steam/steam_install_agreement.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
