# Maintainer : Rob McCathie <korrode at gmail>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: vinz <vinz@archlinux.us>
# Contributor: Aurélien DESBRIÈRES <ice.cube@gmx.com>
# Contributor: Alexandru Ianu <alexandru.ianu at gmail>

_name=thunar-shares-plugin
pkgname=thunar-shares-plugin-manjaro
pkgver=0.2.0.git
_gitcommit=9360911075e9278f7d3ba39d7ea8fe8a3a66b645
pkgrel=4
pkgdesc="Thunar plugin to quickly share a folder using Samba without requiring root access (with auto-setup)"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/${_name}"
license=('GPL2' 'LGPL')
depends=('thunar' 'samba' 'manjaro-settings-samba')
makedepends=('xfce4-dev-tools')
provides=("${_name}=${pkgver}")
conflicts=("${_name}")
install="${pkgname}.install"
source=("http://git.xfce.org/thunar-plugins/${_name}/snapshot/${_name}-${_gitcommit}.tar.bz2")
sha1sums=('14387c9c4d166a6cc689f5abceb6863ccc0eb375')

build() {
  cd "${srcdir}/${_name}-${_gitcommit}"
  ./autogen.sh --prefix=/usr --enable-debug=no
  make
}

package() {
  cd "${srcdir}/${_name}-${_gitcommit}"
  make DESTDIR="${pkgdir}" install
  
  mkdir -p "${pkgdir}/var/lib/samba/usershare"
  chmod 1770 "${pkgdir}/var/lib/samba/usershare"
}
