# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer:  grimi <grimi at poczta dot fm>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
# Contributor: ridikulus_rat <the.ridikulus.rat@gmail.com>

pkgname=spacefm
_pkgname=$pkgname-ng
pkgver=1.0.6
pkgrel=1
pkgdesc='Multi-panel tabbed file manager'
arch=('i686' 'x86_64')
url="https://github.com/Teklad/$_pkgname"
license=('GPL3')
depends=('desktop-file-utils'
         'gtk3'
         'ffmpegthumbnailer'
         'startup-notification')
makedepends=('intltool' 'gettext')
optdepends=('dbus: dbus integration'
            'util-linux: disk eject support'
            'lsof: device processes'
            'wget: plugin download'
            'gksu: perform as root functionality'
            'udevil: mount as non-root user and mount networks'
            'udisks2: mount as non-root user'
            'pmount: mount as non-root user'
            'curlftpfs: mount FTP shares'
            'jmtpfs: mount MTP devices'
            'gphotofs: mount cameras'
            'ifuse: mount your iPhone/iPod Touch'
            'fuseiso: mount ISO files')
source=("$url/archive/$pkgver.tar.gz")
sha512sums=('1f88c1451a0d6b257bfc1f9214b06eb8fa5706a4f33cfdbfad0907813da5c4bea35632fbfa6fa5b41e8c0da86a028b5a55c3776a90cce3c3ec086adf55b2776b')

build() {
    cd $_pkgname-$pkgver
    ./autogen.sh --with-gtk3 --prefix=/usr
    make
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR=$pkgdir install
    rm $pkgdir/usr/bin/spacefm-installer
}
