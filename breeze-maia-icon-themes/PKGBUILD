# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Philip MÃ¼ller <philm@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=breeze-maia-icon-themes
_pkgname=breeze-icons
pkgver=5.33.0
pkgrel=1
pkgdesc='Breeze icon themes with Maia colors'
arch=('any')
url='https://community.kde.org/Frameworks'
#_commit=8968fe82a5ed2bae7d51db0f6d71f61fb468c1f5
license=('LGPL')
makedepends=("extra-cmake-modules>=${pkgver}" 'qt5-base')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$_pkgname-$pkgver.tar.xz"{,.sig}
	'deepin-show-desktop.svg')
md5sums=('d23d9978a72c4fbffc42322f5c19fa23'
         'SKIP'
         '8e4deb556453e0ff6dd5f415fd7aa4c6')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  mkdir build
  cd $srcdir/build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  msg "Adjusting icon colors..."
  cd $pkgdir/usr/share/icons
  find . ! -type l -name "*.svg" -exec sed -i \
	"s/084883/1e6759/g;\
	s/1272a5/0c7869/g;\
	s/147eb8/338676/g;\
	s/2a6bae/2a6b5f/g;\
	s/2f74c8/338676/g;\
	s/2f77c2/1dab8f/g;\
	s/383e51/38514a/g;\
	s/3daee9/32b39a/g;\
	s/4699d7/16a085/g;\
	s/60a5e7/60e7c4/g;\
	s/655c6f/5c6f6a/g;\
	s/6cc1ef/32b39a/g" '{}' \;
  find . -name "index.*" -exec sed -i "s/Breeze/Breeze-Maia/g;s/Maia Dark/Maia-Dark/g" '{}' \;
  mv breeze Breeze-Maia
  mv breeze-dark Breeze-Maia-Dark
  
  ln -s ../../places/32/user-desktop.svg Breeze-Maia/apps/32/deepin-show-desktop.svg
  ln -s ../../places/32/user-desktop.svg Breeze-Maia-Dark/apps/32/deepin-show-desktop.svg
  ln -s utilities-terminal.svg Breeze-Maia/apps/48/deepin-terminal.svg
  ln -s utilities-terminal.svg Breeze-Maia-Dark/apps/48/deepin-terminal.svg
  cp $srcdir/deepin-show-desktop.svg Breeze-Maia/apps/48/deepin-show-desktop.svg
  cp $srcdir/deepin-show-desktop.svg Breeze-Maia-Dark/apps/48/deepin-show-desktop.svg
}

