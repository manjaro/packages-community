# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexander Monakov <amonakov@gmail.com>
# Contributor: FadeMind <fademind@gmail.com>

pkgname=primus
pkgver=20151110
pkgrel=2.1
pkgdesc="Faster OpenGL offloading for Bumblebee"
arch=('i686' 'x86_64')
url="https://github.com/amonakov/primus"
license=('custom:ISC')
depends=('bumblebee')
makedepends=('git')
source=("git://github.com/amonakov/primus.git#commit=d1afbf6"
        "bug178fix.patch")
sha1sums=('SKIP'
          '13f34cbc8e04a49afc834754dcdf81e379f36c3f')

prepare() {
    #FS47688 https://bugs.archlinux.org/task/47688
    #GH-178  https://github.com/amonakov/primus/issues/178
    patch -Np1 -i ${srcdir}/bug178fix.patch
}

build() {
    cd ${pkgname}

    make
}

package() {
    cd ${pkgname}

    install -D "lib/libGL.so.1"         "$pkgdir/usr/lib/primus/libGL.so.1"
    install -D "primusrun"              "$pkgdir/usr/bin/primusrun"
    install -D "primus.bash-completion" "$pkgdir/etc/bash_completion.d/primusrun"
    gzip -9 "primusrun.1"
    install -D "primusrun.1.gz"         "$pkgdir/usr/share/man/man1/primusrun.1.gz"
    install -D -m644 LICENSE.txt        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
