# Maintainer: Stefano Capitani <stefanoatmanajrodotorg>
# Author: Patrick Brisbin <pbrisbin@gmail.com>

pkgname=downgrade
pkgver=6.0.0
pkgrel=1
pkgdesc="Bash script for downgrading one or more packages to a version in your cache (or optionally in the A.R.M.)"
arch=('any')
url="https://github.com/pbrisbin/$pkgname"
license=('GPL')
source=("https://github.com/pbrisbin/$pkgname/archive/v$pkgver.tar.gz")
optdepends=('sudo: for installation via sudo')
md5sums=('66b11e0ec92711df4d64f62876f322bf')

package() {
  local po_file
  
  #Disable A.L.A. only in stable branch

  cd "$pkgname-$pkgver"
  sed -i -e 's,.*DOWNGRADE_FROM_ALA:=1.*, \
  #Disable A.L.A. only in stable branch\
  if [ ! $(pacman-mirrors -aG) == stable ] ; then\
  : "${DOWNGRADE_FROM_ALA:=1}"\
  else\
  echo\
  echo "Downgrading from A.L.A. is disabled on the stable branch. See https://wiki.archlinux.org/index.php/downgrading_packages for more details."\
  : "${DOWNGRADE_FROM_ALA:=0}"\
  fi\
  ,' downgrade

  for po_file in locale/*.po; do
    locale="$(basename "$po_file" .po)"

    mkdir -p "$pkgdir/usr/share/locale/$locale/LC_MESSAGES/"
    msgfmt "$po_file" -o "$pkgdir/usr/share/locale/$locale/LC_MESSAGES/$pkgname.mo"
  done

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
