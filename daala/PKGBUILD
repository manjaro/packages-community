# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Lex Black <autumn-wind at web dot de>

pkgname=daala
pkgver=0.0.r1696.g7278368
pkgrel=1
pkgdesc="next-next-gen video compression technology from Xiph.org, Mozilla and others"
arch=('i686' 'x86_64')
url="https://www.xiph.org/daala/"
license=('BSD')
makedepends=('git'
    'libjpeg-turbo'
    'libogg'
    'libpng'
    'sdl2'
    'wxgtk2'
    'wxgtk3')
checkdepends=('check')
optdepends=("libjpeg-turbo: JPG support"
    "libogg: OGG support"
    "libpng: PNG support"
    "sdl2: For daala example player")
provides=('libdaala')
conflicts=('libdaala')
options=('!libtool')
source=("git+https://git.xiph.org/$pkgname.git")
md5sums=('SKIP')

pkgver() {
    cd $pkgname
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//'
}

build() {
    cd $pkgname
    ./autogen.sh
    ./configure --prefix=/usr --libdir=/usr/lib --enable-analyzer
    make
}

check() {
    cd $pkgname
    make check
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir/" install
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
