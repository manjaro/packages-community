# Based on the file created for Arch Linux by:
# Thorsten Töpper <atsutane-tu@freethoughts.de>
# SpepS <dreamspepser at yahoo dot it>
# Tobias Powalowski <tpowa@archlinux.org>

# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Philip Müller <philm[at]manjaro[dot]org>

_linuxprefix=linux-rt-lts-manjaro
_extramodules=extramodules-4.9-rt-lts-MANJARO
pkgname="$_linuxprefix-ndiswrapper"
_pkgname=ndiswrapper
groups=("$_linuxprefix-extramodules")
pkgver=1.61
pkgrel=13
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors."
license=('GPL')
arch=('x86_64')
url="http://ndiswrapper.sourceforge.net"
depends=("$_linuxprefix" 'wireless_tools' 'perl' "$_pkgname-utils")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver")
install=ndiswrapper.install
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz"
        "kernel-4.0.patch"
        "kernel-4.2.patch")
options=('!strip')
sha256sums=('2ac1847c24cbfa4f48a800b04c9721219614f1663a4ac94af3c7939b45c47584'
            '80f89025a3de4b1decb82550bd89a3b4e43732b8f5104bac6e2e4b66d20d2d57'
            '4019d3f06f69dc1f67b3bf2faa7632ed2d58eb2c22878d52daece0d496bb3810')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i $srcdir/kernel-4.0.patch
  patch -Np1 -i $srcdir/kernel-4.2.patch
}

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  cd "$srcdir/$_pkgname-$pkgver"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile

  # make sure we point to the right build directory
  sed -i "/^KBUILD/ s,.*,KBUILD = $(readlink -f /usr/lib/modules/$_kernver/build)," driver/Makefile

  make KVERS=$_kernver
}

package() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  
  cd "$srcdir/$_pkgname-$pkgver"

  make INST_DIR="usr/lib/modules/$_extramodules" \
    KVERS=$_kernver DESTDIR="$pkgdir/" sbindir=/usr/bin usrsbindir=/usr/bin install

  # set the kernel we've built for inside the install script
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

  gzip "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

  # mv ndiswrapper-utils
  mkdir -p $srcdir/tmp-ndis-utils/usr
  mv -v "$pkgdir/usr/share" "$srcdir/tmp-ndis-utils/usr"
  mv -v "$pkgdir/usr/bin" "$srcdir/tmp-ndis-utils/usr"
}
