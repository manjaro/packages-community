# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

_linuxprefix=linux416-rt
_extramodules=extramodules-4.16-rt-MANJARO
pkgname=$_linuxprefix-r8168
_pkgname=r8168
pkgver=8.045.08
pkgrel=4
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc' "$_linuxprefix")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
install=$_pkgname.install
source=("https://github.com/mtorromeo/$_pkgname/archive/$pkgver/$pkgver.tar.gz"
    'linux-4.15.patch')
sha256sums=('18161cb72fc872a9aed194514f7b8fb8b255b6fa6ed3d2dd459700eaad575f31'
            '1c9f5bd2890bc98b552bd1b7acc91885c4ed013a2c188bfab70ddaae058fa241')

prepare() {
	cd "$_pkgname-$pkgver"
    patch -p1 -i "$srcdir/linux-4.15.patch"
}

build() {
	_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"

	cd "$_pkgname-$pkgver"

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
	  SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
	  EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
	  modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
