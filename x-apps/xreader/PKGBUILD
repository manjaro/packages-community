# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# AUR-Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=1.4.1
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/linuxmint/${pkgname}"
groups=('x-apps')
depends=('desktop-file-utils'
         'djvulibre'
         'ghostscript'
         'gsettings-desktop-schemas'
         'gtk2'
         'gtk3'
         'gtk3-print-backends'
         'libsecret'
         'libspectre'
         'poppler-glib'
         'webkit2gtk')
makedepends=('mate-common' 'yelp-tools')
optdepends=('nemo: nemo extension (must be enabled at compile time)'
    'caja-extensions-common: caja extension (must be enabled at compile time)'
    'texlive-bin: support for dvi files')
source=("${url}/archive/${pkgver}.tar.gz")
md5sums=('d550a2d6d919b83e11c4c90d85ca6945')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/${pkgname}" \
        --disable-caja --disable-nemo \
        --enable-epub
    make
}

package(){
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir/" install
}
