# Maintainer: Fabian Bornschein <plusfabi(At)gmail(Dot)com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
# Contributor: Alexandru Ianu <alexandru@manjaro.org>
# Contributor: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>

pkgname=steam-manjaro
_package=steam
pkgver=1.0.0.54
pkgrel=7
pkgdesc="Valve's digital software delivery system"
url='http://steampowered.com/'
arch=('i686' 'x86_64')
license=('custom')
depends=('lsb-release' 'desktop-file-utils' 'curl' 'dbus' 'freetype2' 'gdk-pixbuf2' 'ttf-font' 'zenity')
depends_x86_64=('lib32-libgl' 'lib32-mesa' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss' 'lib32-alsa-plugins')
optdepends=('steam-native: steam native runtime support')
provides=('steam')
conflicts=('steam')
install=steam-manjaro.install
source=(http://repo.steampowered.com/${_package}/pool/${_package}/s/${_package}/${_package}_${pkgver}.tar.gz
        'steam-manjaro.install'
        'steam-uinput.conf'
        '0001-steam_controller_uaccess.patch'
        '0002-steam_htc_vive_mode0660.patch'
        '0003-alsa-sdl-audiodriver.patch'
        '0004-dbus_fatal_warning.patch'
        '0005-manjaro-steam-native.patch'
# 20170503 (date +%Y%m%d) disable ld_library_path_fallback for now.
#        '0006-ld_library_path_fallback_x86.patch'
#        '0006-ld_library_path_fallback_x86_64.patch'
        )
sha512sums=('1820f596359d829a4e119f1f916a741ecb7973d36684916cdbbfa435fcdc9c288852491fd6d0b064e8154a43c60d8dcce3c7ed9275aed8f5f116c2782489bf32'
            '8f3a781077bba4a1d5d06943060e01a21800d5cfba91262502c47810499c5b9367c10d1c319bb9ba006b143ddc5159f2826c8f3229322bc9fe28c1fcf66ea103'
            'a9b069ed121ffeee887e0583d8cb46035ecf1fa90a26a4ecb3aa11ff03178b2b08621f6676db6b2350f290694c04aabcf36f2ce3e0813a76dde9a33555edb112'
            '41b76c6e35a1e846f4246cba8521c505e1c55cbf2f692d4e7d89198d3093c54fb721207f82e39d2c1e349935c47e5c2f321c28c92cfe1dcd2be745325f94a8c1'
            '4f9e92268f6624e36ae9a987118af0e38bf51b73a4493863a80a558e0807171fc0343131dc197f8fc5bc48452978428092d69a306a2cf2710520baba564a5aaa'
            'ec57f189fbc25633799c553b0eb30bd074f02befd9c7e7ca1c052be0a66ca3745b511b0fccb21ddb57885d60a23c8d40eaa8d63f8da2fc2fff3b8e7f5d23b091'
            '12d8b5786e2a375ec03131ff5c246c64793acc24cf42e74b40cd77a031c17543da05f7dd33b1f17f63524710f5d96636f35908e1953292f44e949f84fb753c27'
            'fec7a63231c502debaa8ccdac73270e47ad42dd85e420eee44100ed0e4e4678e9ad70e36a3df02cc9ebe9936b710737d0867532a9b319bc1bee141c420db5900')

prepare()
{
    cd ${_package}
    patch -Np1 < "${srcdir}/0001-steam_controller_uaccess.patch"
    patch -Np1 < "${srcdir}/0002-steam_htc_vive_mode0660.patch"
    patch -Np1 < "${srcdir}/0003-alsa-sdl-audiodriver.patch"
    patch -Np1 < "${srcdir}/0004-dbus_fatal_warning.patch"
    patch -Np1 < "${srcdir}/0005-manjaro-steam-native.patch"
#    if [[ "$CARCH" == "x86_64" ]]
#        then
#            patch -Np1 < "${srcdir}/0006-ld_library_path_fallback_x86_64.patch"
#        else
#            patch -Np1 < "${srcdir}/0006-ld_library_path_fallback_x86.patch"
#    fi
}

package()
{
    make -C "${_package}" DESTDIR="${pkgdir}" install

    # install license
    install -Dm 644 "${pkgdir}/usr/share/doc/steam/steam_install_agreement.txt" \
    "${pkgdir}/usr/share/licenses/steam/LICENSE"

    #install changelog
    install -Dm 644 "${_package}/debian/changelog" -t "${pkgdir}/usr/share/doc/${_package}"

    # blank steamdeps because apt-get
    ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

    # install udev rules
    install -Dm 644 "${_package}/lib/udev/rules.d/99-steam-controller-perms.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-controller.rules"
    install -Dm 644 "${_package}/lib/udev/rules.d/60-HTC-Vive-perms.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/70-htc-vive.rules"

    # start uinput at boot
    install -Dm 644 "${srcdir}/steam-uinput.conf" \
    "${pkgdir}/usr/lib/modules-load.d/steam-uinput.conf"

    # License
    install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    ln -s "/usr/share/doc/steam/steam_install_agreement.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
