# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tom < tomgparchaur at gmail dot com >
# Contributor: David Manouchehri <d@32t.ca>

pkgname=dropbox
pkgver=25.4.28
pkgrel=1
pkgdesc="A free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=("i686" "x86_64")
url="http://www.dropbox.com"
license=('custom')
depends=('dbus' 'libxmu' 'libxslt')
makedepends=('chrpath' 'desktop-file-utils')
optdepends=('ufw-extras: UFW rules for Dropbox'
            'perl-file-mimeinfo: Opening the Dropbox folder on some desktop environments'
            'kde-servicemenus-dropbox: Servicemenu for Dolphin which allows easy access to most of Dropbox features')
conflicts=("dropbox-experimental")
replaces=("dropbox3" "dropbox2")
options=('!strip')
source=("dropbox"
        "dropbox.desktop"
        "dropbox.png"
        "dropbox.service"
        "dropbox@.service"
        "terms.txt")
source_i686=("$pkgname-lnx.x86-$(date +%Y%m%d).tar.gz::$url/download?plat=lnx.x86")
source_x86_64=("$pkgname-lnx.x86_64-$(date +%Y%m%d).tar.gz::$url/download?plat=lnx.x86_64")
sha256sums=('4240052a07a0f38dbdea6f199d5f1fc7b04ace9bf7474a63f09bdff7682a5154'
            '541f2fd2de0d601a08cde7853e404062f542af21e6e7106825b5e68177168e0f'
            'e7d245f5d1a3d5322614b61400ae2913a8caef44bc86717ff7d8197a15dd7f01'
            '5ea25085d97708d9c9ffe3d0754dab3e09937ba8111a80b67e443dae3c15503e'
            '85ccabbe4e1f2dc67a480bc074d949578b148168a656dcb19860bb9c4f7b3e8a'
            'c39d63bcfc0d40002a5df052298feb79f2e8dd3d93da27ef31eb672b8d2fc70e')
sha256sums_i686=('88188ce5508c121bbc7ff001199f49828eab0cde3bcabf6cac9bcee0a7c12eb6')
sha256sums_x86_64=('83e653e06e2a2f0cdf0cb788408168b8b058472297ea2ea31d59cd7c066d0b64')

if [ "$CARCH" = "x86_64" ]; then
    _source_arch="x86_64"
else
    _source_arch="x86"
fi

prepare() {
    cd .${pkgname}-dist/${pkgname}-lnx.${_source_arch}-*
    rm library.zip
    ln -s ${pkgname} library.zip
    chrpath -d plugins/platforms/libqxcb.so
}

pkgver() {
    cat .$pkgname-dist/VERSION
}

package() {
    cd .${pkgname}-dist
    install -dm755 "$pkgdir"/opt
    cp -dr --no-preserve='ownership' dropbox-lnx.${_source_arch}-${pkgver} "${pkgdir}"/opt/dropbox
    find "${pkgdir}"/opt/dropbox -type d -exec chmod 755 {} +
    find "${pkgdir}"/opt/dropbox -type f -exec chmod 644 {} +
    chmod 755 "${pkgdir}"/opt/dropbox/dropbox{,d}
    install -Dm 755 ../dropbox ${pkgdir}/usr/bin/dropbox

    install -dm 755 "${pkgdir}"/usr/share/{applications,licenses/dropbox,pixmaps}
    install -m 644 ../dropbox.png "${pkgdir}"/usr/share/pixmaps/
    install -m 644 ../terms.txt "${pkgdir}"/usr/share/licenses/dropbox/

    install -dm 755 "${pkgdir}"/usr/lib/systemd/{user,system}
    install -m 644 ../dropbox.service "${pkgdir}"/usr/lib/systemd/user/
    install -m 644 ../dropbox@.service "${pkgdir}"/usr/lib/systemd/system/
    
    desktop-file-install ../dropbox.desktop --dir ${pkgdir}/usr/share/applications/
}
